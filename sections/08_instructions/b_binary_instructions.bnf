// --- [ Binary instructions ] -------------------------------------------------

// ~~~ [ add ] ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

AddInst
	: "add" OverflowFlags ConcreteType Value "," Value OptCommaAttachedMDList   << astx.NewAddInst($2, $3, $5, $6) >>
;

// ~~~ [ fadd ] ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

FAddInst
	: "fadd" FastMathFlags ConcreteType Value "," Value OptCommaAttachedMDList   << astx.NewFAddInst($2, $3, $5, $6) >>
;

// ~~~ [ sub ] ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

SubInst
	: "sub" OverflowFlags ConcreteType Value "," Value OptCommaAttachedMDList   << astx.NewSubInst($2, $3, $5, $6) >>
;

// ~~~ [ fsub ] ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

FSubInst
	: "fsub" FastMathFlags ConcreteType Value "," Value OptCommaAttachedMDList   << astx.NewFSubInst($2, $3, $5, $6) >>
;

// ~~~ [ mul ] ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

MulInst
	: "mul" OverflowFlags ConcreteType Value "," Value OptCommaAttachedMDList   << astx.NewMulInst($2, $3, $5, $6) >>
;

// ~~~ [ fmul ] ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

FMulInst
	: "fmul" FastMathFlags ConcreteType Value "," Value OptCommaAttachedMDList   << astx.NewFMulInst($2, $3, $5, $6) >>
;

// ~~~ [ udiv ] ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

UDivInst
	: "udiv" OptExact ConcreteType Value "," Value OptCommaAttachedMDList   << astx.NewUDivInst($2, $3, $5, $6) >>
;

// ~~~ [ sdiv ] ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

SDivInst
	: "sdiv" OptExact ConcreteType Value "," Value OptCommaAttachedMDList   << astx.NewSDivInst($2, $3, $5, $6) >>
;

// ~~~ [ fdiv ] ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

FDivInst
	: "fdiv" FastMathFlags ConcreteType Value "," Value OptCommaAttachedMDList   << astx.NewFDivInst($2, $3, $5, $6) >>
;

// ~~~ [ urem ] ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

URemInst
	: "urem" ConcreteType Value "," Value OptCommaAttachedMDList   << astx.NewURemInst($1, $2, $4, $5) >>
;

// ~~~ [ srem ] ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

SRemInst
	: "srem" ConcreteType Value "," Value OptCommaAttachedMDList   << astx.NewSRemInst($1, $2, $4, $5) >>
;

// ~~~ [ frem ] ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

FRemInst
	: "frem" FastMathFlags ConcreteType Value "," Value OptCommaAttachedMDList   << astx.NewFRemInst($2, $3, $5, $6) >>
;

OverflowFlags
	: empty
	| OverflowFlagList
;

OverflowFlagList
	: OverflowFlag
	| OverflowFlagList OverflowFlag
;

OverflowFlag
	: "nuw"
	| "nsw"
;

FastMathFlags
	: empty
	| FastMathFlagList
;

FastMathFlagList
	: FastMathFlag
	| FastMathFlagList FastMathFlag
;

// From spec and src of v4.0.
//
// ref: http://llvm.org/docs/LangRef.html#fast-math-flags
FastMathFlag
	: "arcp"
	| "fast"
	| "ninf"
	| "nnan"
	| "nsz"
;

OptExact
	: empty
	| "exact"
;

