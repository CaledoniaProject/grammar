// --- [ Global variables ] ----------------------------------------------------

GlobalDecl
	: GlobalIdent "=" ExternLinkage GlobalOptions1 Immutable FirstClassType GlobalOptions2   << astx.NewGlobalDecl($0, $4, $5, $6) >>
;

GlobalDef
	: GlobalIdent "=" OptLinkage GlobalOptions1 Immutable FirstClassType Constant GlobalOptions2   << astx.NewGlobalDef($0, $4, $5, $6, $7) >>
;

GlobalOptions1
	: OptVisibility OptDLLStorageClass OptTLSModel OptUnnamedAddr OptAddrSpace OptExternallyInitialized
;

OptExternallyInitialized
	: empty
	| ExternallyInitialized
;

ExternallyInitialized
	: "externally_initialized"
;

Immutable
	: "constant"   << true, nil >>
	| "global"     << false, nil >>
;

// TODO: Clean up when the parser generator no longer introduces ambiguities
// through the limitation of 1 rune lookahead.
//
// Structured in this way to allow for naiive 1 rune lookahead parser
// generators.
GlobalOptions2
	: "," Align CommaAttachedMDs   << $2, nil >>
	| CommaAttachedMDs             << $0, nil >>
;

