// --- [ Global variables ] ----------------------------------------------------

// TODO: Clean up when the parser generator no longer introduces ambiguities
// through the limitation of 1 token lookahead.
//
// Structured in this way to allow for naiive 1 token lookahead parser
// generators.
//
// Original production rule.
//
//    GlobalDecl
//       : GlobalIdent "=" ExternLinkage GlobalOptions Immutable FirstClassType OptCommaComdat OptCommaAlign OptCommaAttachedMDList   << astx.NewGlobalDecl($0, $4, $5, $7) >>
//    ;
GlobalDecl
	: GlobalIdent "=" ExternLinkage GlobalOptions Immutable FirstClassType OptCommaAttachedMDList                        << astx.NewGlobalDecl($0, $4, $5, $6) >>
	| GlobalIdent "=" ExternLinkage GlobalOptions Immutable FirstClassType "," Align OptCommaAttachedMDList              << astx.NewGlobalDecl($0, $4, $5, $8) >>
	| GlobalIdent "=" ExternLinkage GlobalOptions Immutable FirstClassType "," Comdat OptCommaAttachedMDList             << astx.NewGlobalDecl($0, $4, $5, $8) >>
	| GlobalIdent "=" ExternLinkage GlobalOptions Immutable FirstClassType "," Comdat "," Align OptCommaAttachedMDList   << astx.NewGlobalDecl($0, $4, $5, $10) >>
;

// TODO: Clean up when the parser generator no longer introduces ambiguities
// through the limitation of 1 token lookahead.
//
// Structured in this way to allow for naiive 1 token lookahead parser
// generators.
//
// Original production rule.
//
//    GlobalDef
//       : GlobalIdent "=" OptLinkage GlobalOptions Immutable FirstClassType Constant OptCommaComdat OptCommaAlign OptCommaAttachedMDList   << astx.NewGlobalDef($0, $4, $5, $6, $8) >>
//    ;
GlobalDef
	: GlobalIdent "=" OptLinkage GlobalOptions Immutable FirstClassType Constant OptCommaAttachedMDList                        << astx.NewGlobalDef($0, $4, $5, $6, $7) >>
	| GlobalIdent "=" OptLinkage GlobalOptions Immutable FirstClassType Constant "," Align OptCommaAttachedMDList              << astx.NewGlobalDef($0, $4, $5, $6, $9) >>
	| GlobalIdent "=" OptLinkage GlobalOptions Immutable FirstClassType Constant "," Comdat OptCommaAttachedMDList             << astx.NewGlobalDef($0, $4, $5, $6, $9) >>
	| GlobalIdent "=" OptLinkage GlobalOptions Immutable FirstClassType Constant "," Comdat "," Align OptCommaAttachedMDList   << astx.NewGlobalDef($0, $4, $5, $6, $11) >>
;

GlobalOptions
	: OptVisibility OptDLLStorageClass OptTLSModel OptUnnamedAddr OptAddrSpace OptExternallyInitialized
;

OptExternallyInitialized
	: empty
	| ExternallyInitialized
;

ExternallyInitialized
	: "externally_initialized"
;

Immutable
	: "constant"   << true, nil >>
	| "global"     << false, nil >>
;

