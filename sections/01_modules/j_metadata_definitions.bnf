// TODO: Rename MetadataID to MDNodeID. ref: ParseMDNodeID

// ref: ParseMDNode
MDNode // used as attached metadata identifier
	: MDNodeVector
	| MetadataID
	| SpecializedMDNode
;

MetadataValue // used as function arguments in call instructions
	: Metadata
	| ConcreteType LocalIdent   << astx.NewValue($0, $1) >>
;

// ref: ParseNamedMetadata
NamedMetadataDef
	: MetadataName "=" "!" "{" MetadataIDs "}"   << astx.NewNamedMetadataDef($0, $4) >>
;

MetadataIDs
	: empty
	| MetadataIDList
;

MetadataIDList
	: MetadataID                      << astx.NewMetadataIDList($0) >>
	| MetadataIDList "," MetadataID   << astx.AppendMetadataID($0, $2) >>
;

// ref: ParseStandaloneMetadata
MetadataDef
	: MetadataID "=" OptDistinct MDNodeVector        << astx.NewMetadataDef($0, $3) >>
	| MetadataID "=" OptDistinct SpecializedMDNode   << astx.NewMetadataDef($0, $3) >>
;

OptDistinct
	: empty
	| "distinct"
;

// ref: ParseMDNodeVector
MDNodeVector
	: "!" "{" MDElements "}"   << astx.NewMetadata($2) >>
;

// => ref: ParseMDNodeVector
MDElements
	: empty
	| MDElementList
;

// => ref: ParseMDNodeVector
MDElementList
	: MDElement                     << astx.NewMetadataNodeList($0) >>
	| MDElementList "," MDElement   << astx.AppendMetadataNode($0, $2) >>
;

// => ref: ParseMDNodeVector
MDElement
	: MDNullLit
	| Metadata
;

// ref: ParseMetadata
Metadata
	// ref: ParseValueAsMetadata
	: Type Constant       << astx.NewConstant($0, $1) >>
	| MetadataID
	| MDNodeVector
	| MDString
	| SpecializedMDNode
;

// ref: ParseMDString
MDString
	: "!" string_lit   << astx.NewMetadataString($1) >>
;

SpecializedMDNode
	: MetadataName "(" MDFields ")"
;

MDFields
	: empty
	| MDFieldList
	| MDValueList
;

MDFieldList
	: MDField
	| MDFieldList "," MDField
;

MDField
	: LabelIdent MDValue
;

MDValueList
	: MDValue
	| MDValueList "," MDValue
;

MDValue
	: int_lit
	| MetadataID
	| string_lit
	| BoolLit
	| MDFlagList
	| MDNullLit
	| Type Constant
;

MDFlagList
	: MDFlag
	| MDFlagList "|" MDFlag
;

MDFlag
	: ident
;

MDNullLit
	: NullConst   << astx.NewValue(&ast.MetadataType{}, $0) >>
;

